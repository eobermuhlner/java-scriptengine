plugins {
    id 'java'
    id 'eclipse'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id 'org.sonarqube' version '2.7'
}

group = 'ch.obermuhlner'
version = '1.1.0'
archivesBaseName = 'java-scriptengine'

sourceCompatibility = '1.8'

ext {
    moduleName = "ch.obermuhlner.scriptengine.java"

	if (!project.hasProperty("ossrhUsername")) {
		ossrhUsername = "undefined"
	}
	if (!project.hasProperty("ossrhPassword")) {
		ossrhPassword = "undefined"
	}
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'

    testCompile("org.assertj:assertj-core:3.11.1")
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "java-scriptengine"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

jar {
    manifest {
        attributes(
				"Automatic-Module-Name": moduleName,
				"Bundle-ManifestVersion": 2,
                "Bundle-Name": archivesBaseName,
                "Bundle-SymbolicName": moduleName,
                "Bundle-Version": version,
                "Export-Package": "ch.obermuhlner.scriptengine.java, ch.obermuhlner.scriptengine.java.constructor, ch.obermuhlner.scriptengine.java.execution, ch.obermuhlner.scriptengine.java.name, ch.obermuhlner.scriptengine.java.util")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
	required {
		gradle.taskGraph.hasTask("uploadArchives")
	}

    sign configurations.archives
}

uploadArchives {
	repositories {
	    mavenDeployer {
	        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

	        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }

	        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	            authentication(userName: ossrhUsername, password: ossrhPassword)
	        }

	        pom.project {
	            name 'Java ScriptEngine'
	            packaging 'jar'
	            // optionally artifactId can be defined here
	            description 'Java script engine for Java as a scripting language.'
	            url 'https://github.com/eobermuhlner/java-scriptengine'

	            scm {
	                connection 'scm:git:git://github.com/java-scriptengine.git'
	                developerConnection 'scm:ssh://github.com:eobermuhlner/java-scriptengine.git'
	                url 'https://github.com/eobermuhlner/java-scriptengine/'
	            }

	            licenses {
	                license {
	                    name 'MIT License'
	                    url 'https://raw.githubusercontent.com/eobermuhlner/java-scriptengine/master/LICENSE.txt'
	                }
	            }

	            developers {
	                developer {
	                    id 'eobermuhlner'
	                    name 'Eric Obermuhlner'
	                    email 'eobermuhlner@gmail.com'
	                }
	            }
	        }
	    }
	}
}

// See http://central.sonatype.org/pages/gradle.html
